plugins {
    id 'java-library'
}
java {
    sourceCompatibility JavaVersion.VERSION_11
    targetCompatibility JavaVersion.VERSION_11
}

sourceSets {
    main {
        resources {
            srcDirs += 'certs'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
//    implementation project(':commonutil')
    implementation libs.mqttv5
    implementation libs.jakarta.mail
    implementation libs.bcprov
    implementation libs.bcpkix
}

tasks.register('runMgMain', JavaExec) {
    group = 'application'
    description = 'Runs the MgMain class'
    dependsOn 'copyDependencies'
    dependsOn 'fatJar'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'mg.mgmap.mgmain.MgMain'
}

tasks.register('copyDependencies', Copy) {
    from configurations.runtimeClasspath
    into "$buildDir/dependencies"
}

tasks.register('fatJar', Jar) {
//    dependsOn ':commonutil:jar'
    dependsOn(build)
    archiveBaseName = 'MgMain-fat'
    manifest {
        attributes 'Main-Class': 'mg.mgmap.mgmain.MgMain'
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        // Exclude signature files that cause the SecurityException
        exclude 'META-INF/*.SF'
        exclude 'META-INF/*.RSA'
        exclude 'META-INF/*.INF'
        exclude 'META-INF/*.DSA'
    }
    with jar
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
